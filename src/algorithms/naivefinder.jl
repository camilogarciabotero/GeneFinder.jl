export NaiveFinder, NaiveFinderLazy

struct NaiveFinder <: GeneFinderMethod end
struct NaiveFinderLazy <: GeneFinderMethod end

"""
    _locationiterator(seq::NucleicSeqOrView{DNAAlphabet{N}}; kwargs...) where {N}

This is an iterator function that uses regular expressions to search the entire ORFI (instead of start and stop codons) in a `LongSequence{DNAAlphabet{4}}` sequence.
    It uses an anonymous function that will find the first regularly expressed ORFI. Then using this anonymous function it creates an iterator that will apply it until there is no other CDS.

!!! note
    As a note of the implementation we want to expand on how the ORFIs are found:

    The expression `(?:[N]{3})*?` serves as the boundary between the start and stop codons. 
    Within this expression, the character class `[N]{3}` captures exactly three occurrences of any character (representing nucleotides using IUPAC codes). 
    This portion functions as the regular codon matches. 
    Since it is enclosed within a non-capturing group `(?:)` and followed by `*?`, it allows for the matching of intermediate codons,
    but with a preference for the smallest number of repetitions. 
    
    In summary, the regular expression `ATG(?:[N]{3})*?T(AG|AA|GA)` identifies patterns that start with "ATG," followed by any number of three-character codons (represented by "N" in the IUPAC code), and ends with a stop codon "TAG," "TAA," or "TGA." This pattern is commonly used to identify potential protein-coding regions within genetic sequences.

    See more about the discussion [here](https://discourse.julialang.org/t/how-to-improve-a-generator-to-be-more-memory-efficient-when-it-is-collected/92932/8?u=camilogarciabotero)

"""
function _locationiterator(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    alternative_start::Bool = false
) where {N}
    regorf = alternative_start ? biore"NTG(?:[N]{3})*?T(AG|AA|GA)"dna : biore"ATG(?:[N]{3})*?T(AG|AA|GA)"dna
    fdr(x) = findfirst(regorf, seq, first(x) + 1)
    # function fdr(x)
    #     offset = first(x)
    #     result = findfirst(regorf, @view seq[offset+1:end])
    #     result === nothing && return nothing
    #     return (first(result) + offset):(last(result) + offset)
    # end
    itr = takewhile(!isnothing, iterated(fdr, findfirst(regorf, seq)))
    return itr
end

@doc raw"""
    NaiveFinder(seq::NucleicSeqOrView{DNAAlphabet{N}}; kwargs...) -> Vector{ORF{F}} where {N,F}

A simple implementation that finds ORFIs in a DNA sequence.

The `NaiveFinder` method takes a LongSequence{DNAAlphabet{4}} sequence and returns a Vector{ORFIs} containing the ORFIs found in the sequence. 
    It searches entire regularly expressed CDS, adding each ORFI it finds to the vector. The function also searches the reverse complement of the sequence, so it finds ORFIs on both strands.
        Extending the starting codons with the `alternative_start = true` will search for ATG, GTG, and TTG.
    Some studies have shown that in *E. coli* (K-12 strain), ATG, GTG and TTG are used 83 %, 14 % and 3 % respectively.
!!! note
    This function has neither ORFIs scoring scheme by default nor length constraints. Thus it might consider `aa"M*"` a posible encoding protein from the resulting ORFIs.

 
# Required Arguments

- `seq::NucleicSeqOrView{DNAAlphabet{N}}`: The nucleic acid sequence to search for ORFIs.

# Keywords Arguments

- `alternative_start::Bool`: If true will pass the extended start codons to search. This will increase 3x the execution time. Default is `false`.
- `minlen::Int64=6`:  Length of the allowed ORFI. Default value allow `aa"M*"` a posible encoding protein from the resulting ORFIs.
. Default value allow `aa"M*"` a posible encoding protein from the resulting ORF
!!! note
    As the scheme is generally a scoring function that at least requires a sequence, one simple scheme is the log-odds ratio score. This score is a log-odds ratio that compares the probability of the sequence generated by a coding model to the probability of the sequence generated by a non-coding model:
    ```math
    S(x) = \sum_{i=1}^{L} \beta_{x_{i}x} = \sum_{i=1} \log \frac{a^{\mathscr{m}_{1}}_{i-1} x_i}{a^{\mathscr{m}_{2}}_{i-1} x_i}
    ```
    If the log-odds ratio exceeds a given threshold (`Î·`), the sequence is considered likely to be coding. See [`lordr`](@ref) for more information about coding creteria.

"""
function NaiveFinder(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    alternative_start::Bool = false,
    minlen::Int64 = 6,
    kwargs...
) where {N}
    seqlen = length(seq)
    orfs = Vector{ORF{NaiveFinder}}()
    
    # Handle the sequence name
    seqname = _varname(seq)
    if seqname === nothing
        seqname = :unnamedseq
    else
        seqname = Symbol(seqname)
    end

    @inbounds for strand in (PSTRAND, NSTRAND)
        s = strand == NSTRAND ? reverse_complement(seq) : seq
        @inbounds for location in @views _locationiterator(s; alternative_start)
            if length(location) >= minlen
                start = strand == PSTRAND ? location.start : seqlen - location.stop + 1
                stop = start + length(location) - 1
                frm = start % 3 == 0 ? 3 : start % 3

                push!(orfs, ORF{NaiveFinder}(seqname, start:stop, strand, Int8(frm), NamedTuple()))
            end
        end
    end
    return sort!(orfs)
end


### NAIVE FINDER LAZY IMPLEMENTATION ###

"""
    _estimate_orf_count(seq::NucleicSeqOrView{DNAAlphabet{N}}; alternative_start::Bool=false) where {N}

Estimate the number of ORFs based on start codon count.

Counts ATG start codons in the sequence using a k-mer iterator.
Returns the count as a heuristic estimate for pre-allocating the ORF vector.
"""
function _estimate_orf_count(seq::NucleicSeqOrView{DNAAlphabet{N}}) where {N}
    # 4^3 = 64 possible 3-mers; only count mer"ATG"d
    # counts = zeros(Int, 64)
    counts = 0
    target = mer"ATG"d
    # target_idx = only(encoded_data(target)) + 1

    for kmer in FwDNAMers{3}(seq)
        if kmer == target
            # @inbounds counts[target_idx] += 1
            counts += 1
        end
    end

    return counts
end


"""
    _search_strand!(orfs::Vector{ORF{NaiveFinderLazy}}, seq::NucleicSeqOrView{DNAAlphabet{N}}, seqname::Symbol, strand::Strand, seqlen::Int, alternative_start::Bool, minlen::Int64) where {N}

Helper function to search for ORFs in a single strand direction.

Avoids code duplication between forward and reverse strand searching.
"""
function _search_strand!(
    orfs::Vector{ORF{NaiveFinderLazy}},
    seq::NucleicSeqOrView{DNAAlphabet{N}},
    seqname::Symbol,
    strand::Strand,
    seqlen::Int,
    alternative_start::Bool,
    minlen::Int64
) where {N}
    @inbounds for location in _locationiterator(seq; alternative_start)
        if length(location) >= minlen
            # Adjust coordinates based on strand
            if strand === PSTRAND
                start, stop = location.start, location.stop
            else
                start = seqlen - location.stop + 1
                stop = seqlen - location.start + 1
            end
            
            frm = start % 3 == 0 ? 3 : start % 3
            push!(orfs, ORF{NaiveFinderLazy}(seqname, start:stop, strand, Int8(frm), NamedTuple()))
        end
    end
end

function NaiveFinderLazy(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    alternative_start::Bool = false,
    minlen::Int64 = 6,
    kwargs...
) where {N}
    seqlen = length(seq)
    
    # Estimate ORF count for smart pre-allocation
    estimated_orfs = _estimate_orf_count(seq)
    orfs = Vector{ORF{NaiveFinderLazy}}()
    sizehint!(orfs, estimated_orfs)
    
    # Handle the sequence name
    seqname = _varname(seq)
    seqname = seqname === nothing ? :unnamedseq : Symbol(seqname)

    # Search forward strand
    _search_strand!(orfs, seq, seqname, PSTRAND, seqlen, alternative_start, minlen)
    
    # Search reverse strand (compute reverse complement once)
    rev_seq = reverse_complement(seq)
    _search_strand!(orfs, rev_seq, seqname, NSTRAND, seqlen, alternative_start, minlen)
    
    sort!(orfs)
    return orfs
end

# function _estimate_orf_count(seq::NucleicSeqOrView{DNAAlphabet{N}}; alternative_start::Bool=false) where {N}
#     atg_query = ExactSearchQuery(dna"ATG", iscompatible)
    
#     # Count ATG in forward and reverse strands
#     count_fwd = count(atg_query, seq)
#     count_rev = count(atg_query, reverse_complement(seq))
#     total_atg = count_fwd + count_rev
    
#     if alternative_start
#         # Add GTG and TTG counts
#         for motif in [dna"GTG", dna"TTG"]
#             query = ExactSearchQuery(motif, iscompatible)
#             total_atg += count(query, seq) + count(query, reverse_complement(seq))
#         end
#     end
    
#     # Heuristic: average ~1.5 ORFs per start codon
#     return max(100, div(total_atg * 3, 2))
# end
# const ATGQ1 = ApproximateSearchQuery(dna"ATG")
# const CATQ1 = ApproximateSearchQuery(dna"GTA")

# function _count_motif(q, seq::NucleicSeqOrView{DNAAlphabet{N}}) where {N}
#     n = 0
#     i = 1
#     while true
#         r = findnext(q, 0, seq, i)
#         r === nothing && return n
#         n += 1
#         i = first(r) + 1
#     end
# end

# function _estimate_orf_count(seq::NucleicSeqOrView{DNAAlphabet{N}}) where {N}
#     return _count_motif(ATGQ1, seq) #+ _count_motif(GTAQ1, seq)
# end