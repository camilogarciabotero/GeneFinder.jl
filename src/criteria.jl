export iscoding, 
    log_odds_ratio_decision_rule, lordr, 
    rbsdr

@doc raw"""
    iscoding(seq::NucleicSeqOrView{DNAAlphabet{N}}; criteria::Function = lordr, kwargs...) -> Bool

Check if the given DNA sequence is likely to be coding based on a scoring scheme/function.

## Scoring Criteria/Functions
- `lordr`: Log-Odds Ratio Decision Rule

# Arguments
- `seq::NucleicSeqOrView{DNAAlphabet{N}}`: The DNA sequence to evaluate.

# Keywords
- `criteria::Function = lordr`: The scoring function to use.
- Additional keyword arguments are passed to the criteria function.

# Example
```julia
using BioSequences, GeneFinder

seq = dna"ATGATGCATGCATGCATGCTAG"
collection = findorfs(seq)

# Check if a specific ORF sequence is coding
orfseq = sequence(collection, 1)
iscoding(orfseq)  # Returns: true or false

# Check multiple ORFs
for i in eachindex(collection)
    s = sequence(collection, i)
    println("ORF $i: ", iscoding(s))
end
```

See also: [`lordr`](@ref), [`findorfs`](@ref), [`sequence`](@ref)
"""
function iscoding(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    criteria::Function = lordr,
    kwargs...
) where {N}
    return criteria(seq; kwargs...)
end

### Actual criteria functions ###

## Log-Odds Ratio Decision Rule

@doc raw"""
    lordr(seq::NucleicSeqOrView{DNAAlphabet{N}}; kwargs...) -> Bool
    log_odds_ratio_decision_rule(seq; kwargs...) -> Bool

Check if a given DNA sequence is likely to be coding based on a log-odds ratio.

The log-odds ratio is a statistical measure used to assess the likelihood of a sequence 
being coding or non-coding. It compares the probability of the sequence generated by a 
coding model to the probability of the sequence generated by a non-coding model. If the 
log-odds ratio exceeds a given threshold (`η`), the sequence is considered likely to be coding.

It is formally described as a decision rule:

```math
S(X) = \log \left( \frac{{P_C(X_1=i_1, \ldots, X_T=i_T)}}{{P_N(X_1=i_1, \ldots, X_T=i_T)}} \right) \begin{cases} > \eta & \Rightarrow \text{{coding}} \\ < \eta & \Rightarrow \text{{noncoding}} \end{cases}
```

# Arguments
- `seq::NucleicSeqOrView{DNAAlphabet{N}}`: The DNA sequence to be evaluated.

# Keywords
- `modela::BioMarkovChain = ECOLICDS`: The transition model for coding regions.
- `modelb::BioMarkovChain = ECOLINOCDS`: The transition model for non-coding regions.
- `b::Number = 2`: The base of the logarithm used to calculate the log-odds ratio.
- `η::Float64 = 5e-3`: The threshold value for the log-odds ratio.

# Returns
- `true` if the sequence is likely to be coding.
- `false` if the sequence is likely to be non-coding.

# Example
```julia
using BioSequences, GeneFinder

seq = dna"ATGATGCATGCATGCATGCTAG"
collection = findorfs(seq)

# Check if first ORF is coding
orfseq = sequence(collection, 1)
lordr(orfseq)  # Returns: true or false

# With custom threshold
lordr(orfseq; η=1e-10)
```

See also: [`iscoding`](@ref), [`sequence`](@ref)
"""
function lordr(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    modela::BioMarkovChain = ECOLICDS,
    modelb::BioMarkovChain = ECOLINOCDS,
    b::Number = 2,
    η::Float64 = 5e-3
) where {N}
    scorea = log_odds_ratio_score(seq; modela=modela, b=b)
    scoreb = log_odds_ratio_score(seq; modela=modelb, b=b)
    
    logodds = scorea / scoreb

    return logodds > η
end

const log_odds_ratio_decision_rule = lordr

## Ribosome Binding Site Decision Rule

"""
    rbsdr(seq::NucleicSeqOrView{DNAAlphabet{N}}; kwargs...) -> Bool
    ribosome_binding_site_decision_rule(seq; kwargs...) -> Bool

Evaluates if a DNA sequence has a significant ribosome binding site (RBS).

!!! warning
    This function is currently a placeholder and requires the upstream sequence
    context to properly evaluate RBS presence. Consider using `lordr` instead.

# Arguments
- `seq::NucleicSeqOrView{DNAAlphabet{N}}`: The DNA sequence to evaluate.

# Returns
- `Bool`: `true` if the RBS score exceeds threshold, `false` otherwise.

See also: [`iscoding`](@ref), [`lordr`](@ref)
"""
function rbsdr(
    seq::NucleicSeqOrView{DNAAlphabet{N}};
    kwargs...
) where {N}
    # Note: RBS evaluation typically requires upstream sequence context
    # This is a placeholder - proper implementation needs genomic context
    @warn "rbsdr requires upstream sequence context for proper RBS evaluation" maxlog=1
    return false
end

const ribosome_binding_site_decision_rule = rbsdr